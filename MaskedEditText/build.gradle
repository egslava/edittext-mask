plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

String projectVersion = "1.0.5"
String projectGroup = "ru.egslava"

version = projectVersion
group = projectGroup

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 28
        versionCode 1
        versionName projectVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
}

dependencies {
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'

    testImplementation 'junit:junit:4.12'
}


// bintray deploy ....
String projectName = "edittext-mask"
String projectDescription = new File("README.md").text
String webUrl = "https://github.com/egslava/edittext-mask"
String gitUrl = "https://github.com/egslava/edittext-mask.git"


install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                
                name projectName
                description projectDescription
                url webUrl

                inceptionYear '2017' // HARDCODED

                licenses {
                    license {
                        name 'MIT'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url webUrl
                }
                developers {
                    developer {
                        id 'egslava'
                        name 'Slava Egorenkov'
                        email 'egslava@gmail.com'
                    }
                }
            }
        }
    }
}

// The end of the gradle file is for JCenter publication.
// The original code was written with look at Alexander Matveychuk's code
// and
// https://www.virag.si/2015/01/publishing-gradle-android-library-to-jcenter/

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = System.getenv('bintrayUser')
    key = System.getenv('bintrayApikey')

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = projectName
        userOrg = user
//        projectUrl = webUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        group = projectGroup
        publish = true

        version {
            name = projectVersion
//            desc = projectDescription
            vcsTag = projectVersion
        }
    }
}